namespace Rosella { namespace Query
{
    /* Queryable Class
        This class is a wrapper type which provides higher-order behaviors
        for existing aggregate types. To provide this behavior, we pair each
        aggregate with a Provider. The Provider implements all the behaviors.
    */
    class Queryable
    {
        var data;
        var provider;

        // Constructor. Store the data and get a suitable provider. It is
        // probably better to use Rosella.Query.as_queryable instead of
        // creating this directly.
        function Queryable(var data, var provider [optional], int has_p [opt_flag])
        {
            self.data = data;
            if (has_p)
                self.provider = provider;
            else
                self.provider = self.get_provider(data);
        }

        // Get suitable provider for the given data
        function get_provider(var data)
        {
            var provider = null;
            using Rosella.Query.is_array;
            if (is_array(data)) {
                using Rosella.Query.Provider.get_array_provider;
                provider = get_array_provider();
            } else {
                using Rosella.Query.is_hash;
                if (is_hash(data)) {
                    using Rosella.Query.Provider.get_hash_provider;
                    provider = get_hash_provider();
                }
            }
            if (provider == null) {
                using Rosella.Query.Provider.get_scalar_provider;
                provider = get_scalar_provider(data);
            }
            return provider;
        }

        // Get the raw data back
        function data()
        {
            return self.data;
        }

        // Wrap the given data in a Queryable. In the default case, create a
        // new Queryable
        function as_queryable(var data)
        {
            return new Rosella.Query.Queryable(data);
        }

        // Return a new aggregate where each element is a function of the
        // elements in the data
        function map(var mapper)
        {
            var data = self.provider.map(self.data, mapper);
            return self.as_queryable(data);
        }

        // Return a new aggregate where only the elements from the original
        // data which satisfy a test are included
        function filter(var func)
        {
            var data = self.provider.filter(self.data, func);
            return self.as_queryable(data);
        }

        // Combine elements from the aggregate into a single value and
        // return that.
        function fold(var func, var seed [optional], int has_seed [opt_flag])
        {
            if (!has_seed)
                seed = null;
            var value = self.provider.fold(seed, self.data, func);
            return self.as_queryable(value);
        }

        function sort(var func)
        {
            var result = self.provider.sort(self.data, func);
            return self.as_queryable(result);
        }

        // Return a count of elements
        function count(var func [optional], int has_func [opt_flag])
        {
            if (has_func)
                return self.provider.count(self.data, func);
            else
                return self.provider.count(self.data);
        }

        // Return an aggregate of the first N elements in the data. If a
        // predicate is provided, return the first N which satisfy the
        // prediate
        function take(int count, var func [optional], int has_func [opt_flag])
        {
            var data;
            if (has_func)
                data = self.provider.take(self.data, count, func);
            else
                data = self.provider.take(self.data, count);
            return self.as_queryable(data);
        }

        // Return all but the first N elements. If a predicate is provided,
        // return all by the first N elements which satisfy the predicate.
        function skip(int count, var func [optional], int has_func [opt_flag])
        {
            var data;
            if (has_func)
                data = self.provider.skip(self.data, count, func);
            else
                data = self.provider.skip(self.data, count);
            return self.as_queryable(data);
        }

        //function combine() { }

        // Return the data as an array
        function to_array()
        {
            var data = self.provider.to_array(self.data);
            return self.as_queryable(data);
        }

        // Return the data as a hash
        function to_hash(var func)
        {
            var data = self.provider.to_hash(self.data, func);
            return self.as_queryable(data);
        }

        // Get an iterator over the data
        function get_iter[vtable]()
        {
            var iter = null;
            var data = self.data;
            ${ iter iter, data };
            return data;
        }
    }
}}
