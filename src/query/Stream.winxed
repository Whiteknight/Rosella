namespace Rosella.Query
{
    class Stream : Rosella.Query
    {
        var data;
        var last_stage;
        var source_stage;
        var output_cache;
        var input_cache;

        function Stream(var data)
        {
            self.data = data;
            self.source_stage = self.last_stage = new Rosella.Query.Stage.Source(data);
            self.output_cache = [];
            self.input_cache = null;
        }

        function Stream(var parent, var stage)
        {
            self.data = parent.data;
            self.source_stage = parent.source_stage;
            self.output_cache = [];
            self.last_stage = stage;
            self.input_cache = parent.output_cache;
        }

        /* Data Manipulation Routines
        */

        // Get the raw data back
        function data()
        {
            var d = [];
            :(int is_valid, var item) = self.get_next();
            while (is_valid) {
                push(d, item);
                :(is_valid, item) = self.get_next();
            }
            return d;
        }

        // Run the stream to the end, ignores return values;
        function execute()
        {
            :(int is_valid, var item) = self.get_next();
            while (is_valid)
                :(is_valid, item) = self.get_next();
        }

        function on_iterator(var f)
        {
            return f(self.source_stage.iterator);
        }

        function on_data(var f)
        {
            return f(self.data);
        }

        function has_more()
        {
            if (elements(self.output_cache) > 0)
                return true;
            :(int is_valid, var item) = self.get_next();
            if (is_valid)
                push(self.output_cache, item);
            return is_valid;
        }

        function next()
        {
            if (elements(self.output_cache) > 0)
                return self.output_cache.shift();
            int is_valid;
            var item;
            :(is_valid, item) = self.get_next();
            if (!is_valid)
                Rosella.Error.invalid(__FUNCTION__, "Cannot get next from empty iterable");
            return item;
        }

        /* Delayed Execution Query Routines
        */

        // Perform some action for every element in the aggregate
        function foreach(var func)
        {
            var last_stage = new Rosella.Query.Stage.ForEach(self.last_stage, func);
            return new Rosella.Query.Stream(self, last_stage);
        }

        // Perform a callback on the current value of the data
        function tap(var func)
        {
            var last_stage = new Rosella.Query.Stage.Tap(self.last_stage, func);
            return new Rosella.Query.Stream(self, last_stage);
        }

        // Return a new aggregate where each element is a function of the
        // elements in the data
        function map(var func)
        {
            var last_stage = new Rosella.Query.Stage.Map(self.last_stage, func);
            return new Rosella.Query.Stream(self, last_stage);
        }

        // Return a new aggregate where only the elements from the original
        // data which satisfy a test are included
        function filter(var func)
        {
            var last_stage = new Rosella.Query.Stage.Filter(self.last_stage, func);
            return new Rosella.Query.Stream(self, last_stage);
        }

        // Combine elements from the aggregate into a single value and
        // return that.
        function fold(var func, var seed [optional], int has_seed [opt_flag])
        {
            if (!has_seed)
                seed = null;
            var last_stage = new Rosella.Query.Stage.Fold(self.last_stage, func, seed);
            return new Rosella.Query.Stream(self, last_stage);
        }

        // Sort the data using a given comparison routine
        function sort(var func)
        {
            var last_stage = new Rosella.Query.Stage.Sort(self.last_stage, func);
            return new Rosella.Query.Stream(self, last_stage);
        }

        // Return a count of elements
        function count(var func [optional], int has_func [opt_flag])
        {
            // TODO
        }

        // Return true if any element in the aggregate satisfies the predicate
        // false otherwise. Short-circuits
        function any(var func [optional], int has_func [opt_flag])
        {
            // TODO
        }

        // Return an aggregate of the first N elements in the data. If a
        // predicate is provided, return the first N which satisfy the
        // prediate
        function take(int count, var func [optional], int has_func [opt_flag])
        {
            if (!has_func)
                func = null;
            var last_stage = new Rosella.Query.Stage.Take(self.last_stage, func, count);
            return new Rosella.Query.Stream(self, last_stage);
        }

        // Return all but the first N elements. If a predicate is provided,
        // return all by the first N elements which satisfy the predicate.
        function skip(int count, var func [optional], int has_func [opt_flag])
        {
            if (!has_func)
                func = null;
            var last_stage = new Rosella.Query.Stage.Skip(self.last_stage, func, count);
            return new Rosella.Query.Stream(self, last_stage);
        }

        function flatten()
        {
            var last_stage = new Rosella.Query.Stage.Flatten(self.last_stage);
            return new Rosella.Query.Stream(self, last_stage);
        }

        function append(var s)
        {
            // TODO
        }

        function project(var func)
        {
            var last_stage = new Rosella.Query.Stage.Project(self.last_stage, func);
            return new Rosella.Query.Stream(self, last_stage);
        }

        /* Vtables and Low-level access
        */

        // Get an iterator over the data
        function get_iter[vtable]()
        {
            return new Rosella.Query.Stream.Iterator(self);
        }

        /* Internal Helper Routines
        */

        function get_next()
        {
            if (self.input_cache != null && elements(self.input_cache) > 0)
                return true, self.input_cache.shift();
            :(int is_valid, var value) = self.last_stage.next();
            return is_valid, value;
        }
    }
}
