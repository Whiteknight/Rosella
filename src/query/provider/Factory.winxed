/* Provider Factory Class
    This type is a factory for Query Providers. It looks up appropriate
    providers based on either role or type.
*/
class Rosella.Query.Provider.Factory : Rosella.ObjectFactory
{
    var role_map;           // Mapping of roles to providers
    var type_map;           // Mapping of specific types to providers
    var default_provider;   // Default provider to use if all else fails

    // Constructor. The mappings are provided.
    function Factory(var default_provider, var role_map, var type_map)
    {
        self.default_provider = default_provider;
        self.role_map = role_map;
        self.type_map = type_map;
    }

    // Create the provider for the given data
    function create(var data, var p [slurpy], var n [slurpy,named])
    {
        for (string role in self.role_map) {
            if (does(data, role))
                return self.role_map[role];
        }
        for (var type in self.type_map) {
            if (isa(data, type))
                return self.type_map[type];
        }
        return self.default_provider;
    }

    // Add an additional role mapping
    function set_role_mapping(string role, var provider)
    {
        self.role_map[role] = provider;
    }
}
