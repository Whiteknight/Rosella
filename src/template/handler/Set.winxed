/* Temporary Variable Set
    Set a value into a temporary variable
*/
class Rosella.Template.Handler.Set : Rosella.Template.Handler.Children
{
    var varname;
    var sourcename;
    var children;

    // Constructor. Check syntax.
    function Set(var tokens)
    {
        int numtks = elements(tokens);
        string varname = tokens[0];
        self.varname = varname;
        if (substr(varname, 0, 2) == "__" && substr(varname, length(varname) - 2, 2) == "__")
            Rosella.Error.invalid(__FUNCTION__, "Variable names starting and ending with double underscores are reserved");

        if (numtks == 1) {
            self.sourcename = null;
            self.Children();
        }
        else if (numtks == 3 && tokens[1] == "as")
            self.sourcename = tokens[2];
        else
            Rosella.Error.invalid(__FUNCTION__, "Syntax of set is 'set <var>' or 'set <var> as <value>'");
    }

    // There are two forms of set. 1-argument set can have children,
    // 3-argument set cannot
    function can_have_children()
    {
        if (self.children == null)
            return 0;
        return 1;
    }

    // Render. This node type doesn't have any output. Rendering it
    // performs the assignment
    function render(var engine, var ctx, var sb)
    {
        if (self.sourcename == null && self.children != null) {
            var inner_sb = new "StringBuilder";
            self.__render_children(engine, ctx, inner_sb);
            string text = string(inner_sb);
            ctx.set_temporary(self.varname, text);
        }
        else if (self.sourcename != null) {
            var value = ctx.get_value(self.sourcename);
            ctx.set_temporary(self.varname, value);
        }
    }

    function to_winxed(var engine, var builder)
    {
        if (self.sourcename == null && self.children != null) {
            string code = <<:
    {
    push(__BUILDER_STACK__, __BUILDER__);
    __BUILDER__ = new 'StringBuilder';
:>>
;
            push(builder, code);
            self.__to_winxed_children(engine, builder);
            code = <<:
    __CONTEXT__.set_temporary('%s', string(__BUILDER__));
    __BUILDER__ = __BUILDER_STACK__.pop();
    }
:>>
;
            push(builder, code);
        } else if (self.sourcename != null) {
            string code = "\t__CONTEXT__.set_temporary('" + string(self.varname) + "', __CONTEXT__.get_value('" + string(self.sourcename) + "'));\n";
            push(builder, code);
        }
    }
}
