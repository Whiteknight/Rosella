/* Sub-Template including
    Include template text from another file.
*/
class Rosella.Template.Handler.Include : Rosella.Template.Handler
{
    var filename;

    // Constructor
    function Include(var tokens) { self.Handler(tokens); }

    function parse(var tokens)
    {
        if (elements(tokens) != 1)
            Rosella.Error.error("Include requires exactly 1 argument");
        self.filename = tokens[0];
    }

    // Get template text from the file and pass it to the template
    // engine
    function render(var engine, var ctx, var sb)
    {
        string output = engine.generate_from_file(self.filename, ctx);
        push(sb, output);
    }

    // Compile to winxed
    function to_winxed(var engine, var builder)
    {
        push(builder, "\n    // Begin template from '" + string(self.filename) + "'\n");
        engine.compile_to_winxed_file_recurse(self.filename, builder, 1:[named("recurse")]);
        push(builder, "    // End template from '" + string(self.filename) + "'\n\n");
    }
}
