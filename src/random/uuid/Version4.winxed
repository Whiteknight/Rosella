/* Version 4 UUID
    This algorithm uses the PRNG to generate a Version4 UUID. These are not
    guaranteed to be unique, though the sample space is so large that collisions
    are unlikely.
*/
class Rosella.Random.UUID.Version4 : Rosella.Random.UUID
{
    // Constructor. Create a new UUID
    function Version4()
    {
        self.value = self.__get();
    }

    // Constructor. Create a new UUID object from a given string representation
    function Version4(string uuid)
    {
        if (!Rosella.Random.UUID.is_uuid(uuid))
            Rosella.Error.error("UUID must be in format xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx");
        self.value = uuid;
    }

    // These are "Version 4" UUIDs
    function type_name() { return "UUID Version 4"; }

    // Get a new UUID using the default PRNG
    function __get()
    {
        var rand = Rosella.Random.default_uniform_random();
        string hex[] = ["0", "1", "2", "3", "4", "5", "6", "7", "8", "9", "A", "B", "C", "D", "E", "F"];
        string s[] = [];
        for (int i = 0; i < 36; i++)
            s[i] = hex[int(rand.get_range(0, 16))];
        s[14] = hex[4];
        int s19 = (int(rand.get_range(0, 16)) & 0x03) | 0x08;
        s[19] = hex[s19];
        s[8] = s[13] = s[18] = s[23] = "-";
        return join("", s);
    }
}
