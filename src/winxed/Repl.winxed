namespace Rosella { namespace Winxed
{
    class Repl
    {
        var compiler;
        var count;
        var commands;

        function Repl()
        {
            self.compiler = load_language("winxed");
            self.count = 0;
            self.commands = {
                "quit" : function (var state, string args) { state.stop(); },
                "help" : function (var state, string args) { say("Just type some winxed code!"); },
                "codeon" : function (var state, string args) { state.showcode = 1; },
                "codeoff" : function (var state, string args) { state.showcode = 0; },
                "addpre" : function(var state, string args) { push(state.preamble, args); },
                "showpre" : function(var state, string args) {
                    for (int i = 0; i < elements(state.preamble); i++)
                        say(sprintf("\t%d: %s", [i, state.preamble[i]]));
                }
            };
        }

        function run()
        {
            var state = new Rosella.Winxed.ReplState();
            var stdin = getstdin();

            while (state.keep_running)
            {
                var input = stdin.readline_interactive("\nWinxed-Rosella> ");
                if (input == null)
                    break;
                if (!self.run_command(input, state)) {
                    var result = self.eval(input, state);
                    self.print_result(result, state);
                }
            }
        }

        function run_command(string input, var state)
        {
            if (substr(input, 0, 1) != "%")
                return 0;

            input = substr(input, 1);
            int idx = 0;
            int len = length(input);
            ${ find_cclass idx, 0x0020, input, 0, len };
            string cmd = "";
            string args = "";
            if (idx == len)
                cmd = input;
            else {
                cmd = substr(input, 0, idx);
                idx = idx + 1;
                args = substr(input, idx);
            }
            if (exists self.commands[cmd]) {
                var func = self.commands[cmd];
                func(state, args);
                return 1;
            }
            return 0;
        }

        function print_result(var result, var state)
        {
            if (result == null)
                return;
            int does_array = 0;
            ${ does does_array, result, "array" };
            if (does_array)
                self.print_array_result(result, state);

            int does_hash = 0;
            ${ does does_hash, result, "hash" };
            if (does_hash)
                self.print_hash_result(result, state);

            if (!does_array && !does_hash)
                self.print_scalar_result(result, state);
        }

        function print_array_result(var result, var state)
        {
            say("\t>>> Result = [");
            for (int i = 0; i < elements(result); i++)
                say(sprintf("\t>>>\t%d => '%s' (%s)", [i, result[i], string(typeof(result[i]))]));
            say("\t>>> ]");
        }

        function print_hash_result(var result, var state)
        {
            self.sprintf("Result = {");
            for (string key in result)
                self.sprintf("\t'%s' => '%s' (%s)", key, result[key], string(typeof(result[key])));
            self.sprintf("}");
        }

        function print_scalar_result(var result, var state)
        {
            self.sprintf("Result = '%s'", result);
        }

        function eval(string code, var state)
        {
            var func = self.compile(code, state);
            try {
                return func(state);
            } catch (e) {
                say("Error: " + string(e["message"]));
                return null;
            }
        }

        function compile(string code, var state)
        {
            self.count = self.count + 1;
            string preamble = join(";\n\t", state.preamble);
            string func_c = <<:
function __REPL_line_%d(var state) {
    var result = null;
    %s;
    %s;
    return result;
}
:>>
;
            func_c = sprintf(func_c, [self.count, preamble, code]);
            if (state.showcode) {
                say(">>> CODE START <<<");
                say(func_c);
                say(">>> END CODE <<<");
            }
            return self.compiler.compile(func_c);
        }

        function sprintf(string fmt, var args [slurpy])
        {
            print("\t>>> ");
            say(sprintf(fmt, args));
        }
    }
}}
