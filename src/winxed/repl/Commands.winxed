namespace Rosella { namespace Winxed { namespace Repl
{
    class CommandEngine
    {
        function CommandEngine()
        {
        }

        function try_run_command(string input, var state)
        {
            if (substr(input, 0, 1) != "%")
                return 0;

            input = substr(input, 1);
            int idx = 0;
            int len = length(input);
            ${ find_cclass idx, 0x0020, input, 0, len };
            string cmd = "";
            string args = "";
            if (idx == len)
                cmd = input;
            else {
                cmd = substr(input, 0, idx);
                idx = idx + 1;
                args = substr(input, idx);
            }
            return self.run_command(cmd, args, state);
        }

        function run_command(string cmd, string args, var state)
        {
            var func_ns = null;
            var ns_name = ["Rosella", "Winxed", "Repl", "CommandFuncs"];
            ${ get_hll_namespace func_ns, ns_name };
            var func = func_ns.find_sub(cmd);
            if (func == null)
                return 0;
            func(state, args);
            return 1;
        }
    }
}}}
