namespace Rosella
{
    class Path
    {
        var separator;
        var searchers;

        function BUILD(var searchers [slurpy],
                string separator [optional,named], int has_sep [opt_flag])
        {
            using Rosella.build;

            if (has_sep)
                self.separator = separator;
            else
                self.separator = ".";
            if (elements(searchers) == 0) {
                self.searchers = [];
                self.searchers[0] = build(class Rosella.Path.Searcher.Hash);
                self.searchers[1] = build(class Rosella.Path.Searcher.Attribute);
            }
            else
                self.searchers = searchers;
        }

        function result_is_valid(var result)
        {
            if (result == null)
                return 0;
            int is_defined = 0;
            ${ defined is_defined, result };
            return is_defined;
        }

        function get(var obj, string path)
        {
            if (path == null || path == "") {
                say("found immediately");
                return obj;
            }

            string next_path = path;
            string search = path;
            string remainder = "";
            int idx = length(path) - 1;

            while (idx > 0) {
                int success = 0;
                var result = null;

                for (var searcher in self.searchers) {
                    if (!searcher.can_search(obj))
                        continue;
                    var result;
                    :(success, result) = searcher.search(obj, search);
                    if (success) {
                        obj = result;
                        break;
                    }
                }

                if (success) {
                    if (remainder == null || remainder == "")
                        return obj;
                    // Lock in the result;
                    search = remainder;
                    next_path = remainder;
                    remainder = "";
                } else {
                    idx = search.reverse_index(self.separator, 0);
                    search = substr(search, 0, idx);
                    remainder = substr(next_path, idx + 1);
                }
            }

            return null;
        }
    }
}
