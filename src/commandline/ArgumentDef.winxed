/* Argument Definitions.
    Represents the forward declarations of the different arguments. These
    definitions are used during argument processing to parse out expected
    arguments.
*/
class Rosella.CommandLine.ArgumentDef
{
    // TODO: We might consider breaking this up into different strategy subclasses
    // depending on raw_defs format, and adding a factory to choose between
    // them

    var raw_defs;       // The raw argument definitions from the user
    var prepared_defs;  // The prepared/processed argument definitions
    var aliases;        // Map of aliased argument names

    // Constructor
    function ArgumentDef(var raw_defs)
    {
        self.raw_defs = raw_defs;
    }

    /* Public Methods
    */

    function get_definition(string arg_name)
    {
        :(var arg_defs, var aliases) = self.__get_definitions();
        if (exists arg_defs[arg_name])
            return arg_name, arg_defs[arg_name];
        if (exists aliases[arg_name]) {
            string alias = aliases[arg_name];
            return alias, arg_defs[alias];
        }
        Rosella.Error.error("Unknown argument '%s'", arg_name);
    }

    // Get a description or help message. This message will show all argument
    // definitions and the description message associated with each
    function get_description(var sb = new 'StringBuilder')
    {
        var raw_defs = self.raw_defs;
        if (does(raw_defs, "hash"))
            self.__get_description_hash(raw_defs, sb);
        else if (does(raw_defs, "array"))
            self.__get_description_array(raw_defs, sb);
        else
            Rosella.Error.error("Unknown argument definition type '%s'", typeof(raw_defs));
        return sb;
    }

    // Process the input definitions format into a format suitable to use for
    // argument processing.
    function __get_definitions()
    {
        if (self.prepared_defs != null)
            return self.prepared_defs, self.aliases;

        var arg_defs = Rosella.get_string_string_hash();
        var aliases  = Rosella.get_string_string_hash();
        var raw_defs = self.raw_defs;

        if (does(raw_defs, "hash"))
            self.__prepare_arg_defs_hash(raw_defs, arg_defs, aliases);
        else if (does(raw_defs, "array"))
            self.__prepare_arg_defs_array(raw_defs, arg_defs, aliases);
        else
            Rosella.Error.error("Unknown argument definition type '%s'", typeof(raw_defs));

        self.prepared_defs = arg_defs;
        self.aliases = aliases;
        return arg_defs, aliases;
    }

    /* Private Helper Methods
    */

    // Get a description message for a new-style def hash
    function __get_description_hash(var raw_defs, var sb)
    {
        int max = 0;
        var fixed_defs = {};
        for (string arg in raw_defs) {
            var parts = split("=", arg);
            string arg_name = parts[0];
            string prefix = (length(arg_name) == 1) ? "-" : "--";
            arg_name = prefix + arg;
            fixed_defs[arg_name] = raw_defs[arg];

            int len = length(arg_name);
            if (len > max)
                max = len;
        }
        max += 4;

        for (string arg in fixed_defs) {
            string desc = fixed_defs[arg];
            push(sb, "    ");

            push(sb, arg);
            push(sb, repeat_string(" ", max - length(arg)));
            push(sb, desc);
            push(sb, "\n");
        }
        return sb;
    }

    // Get a description message for an old-style array of arrays
    function __get_description_array(var raw_defs, var sb)
    {
        // TODO
    }

    /* We can handle defs of the following types:
        foo|f       f|foo|foobar, etc
        foo         f
        foo=s       f=s   (b, i, f, @ or % too)
        foo=[]      f=[]
        foo={}      f={}
    */
    // Get a processed definition hash for a new-style hash
    function __prepare_arg_defs_hash(var defs, var arg_defs, var aliases)
    {
        for (string arg_def in defs) {
            string arg_name;
            string arg_type = "b";  // Default is a boolean flag
            var raw_aliases = null;

            // Separate on the '=' the name(s) from the type
            int eql_idx = index_of_substr(arg_def, "=");
            if (eql_idx != -1) {
                arg_type = substr(arg_def, eql_idx + 1);
                arg_def = substr(arg_def, 0, eql_idx);
            }

            // Different aliases for the same option are separated by '|'.
            // Get the list of them and take one of them to be the canonical
            // arg_name.
            if (index_of_substr(arg_def, "|") != -1) {
                raw_aliases = split("|", arg_def);
                arg_name = raw_aliases.pop();
            } else
                arg_name = arg_def;

            // Verify that the type is acceptable
            arg_type = self.__verify_arg_type(arg_name, arg_type);

            // Add all aliases to the list of aliases
            arg_defs[arg_name] = arg_type;
            if (raw_aliases != null) {
                for (string alias in raw_aliases)
                    aliases[alias] = arg_name;
            }
        }
    }

    // Get a processed definition hash for an old-style array of arrays
    function __prepare_arg_defs_array(var defs, var arg_defs, var aliases)
    {
        // TODO
    }

    function __verify_arg_type(string arg_name, string arg_type)
    {
        switch (arg_type) {
            case "b":   // Boolean Flag
            case "s":   // String
            case "i":   // Integer
            case "f":   // Float
            case "[]":  // Array
            case "{}":  // Hash
                // These are OK
                break;
            case "@":   // Old-style Array
                arg_type = "[]";
                break;
            case "%":   // Old-style Hash
                arg_type = "{}";
                break;
            default:
                Rosella.Error.error("Unknown argument type for '%s': '%s'", arg_name, arg_type);
        }
        return arg_type;
    }
}
