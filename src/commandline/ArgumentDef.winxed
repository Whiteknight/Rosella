/* Argument Definitions.
    Represents the forward declarations of the different arguments. These
    definitions are used during argument processing to parse out expected
    arguments.
*/
class Rosella.CommandLine.ArgumentDef
{
    // TODO: We might consider breaking this up into different strategy subclasses
    // depending on raw_defs format, and adding a factory to choose between
    // them

    var raw_defs;       // The raw argument definitions from the user
    var prepared_defs;  // The prepared/processed argument definitions

    // Constructor
    function ArgumentDef(var raw_defs)
    {
        self.raw_defs = raw_defs;
    }

    /* Public Methods
    */

    // Get a description or help message. This message will show all argument
    // definitions and the description message associated with each
    function get_description(var sb = new 'StringBuilder')
    {
        // TODO: Prepend a usage string with program name
        var raw_defs = self.raw_defs;
        if (does(raw_defs, "hash"))
            self.__get_description_hash(raw_defs, sb);
        else if (does(raw_defs, "array"))
            self.__get_description_array(raw_defs, sb);
        else
            Rosella.Error.error("Unknown argument definition type '%s'", typeof(raw_defs));
        return sb;
    }

    // Process the input definitions format into a format suitable to use for
    // argument processing.
    function get_definitions()
    {
        if (self.prepared_defs != null)
            return self.prepared_defs;

        var arg_defs = Rosella.get_string_string_hash();
        var raw_defs = self.raw_defs;
        if (does(raw_defs, "hash"))
            self.__prepare_arg_defs_hash(raw_defs, arg_defs);
        else if (does(raw_defs, "array"))
            self.__prepare_arg_defs_array(raw_defs, arg_defs);
        else
            Rosella.Error.error("Unknown argument definition type '%s'", typeof(raw_defs));
        self.prepared_defs = arg_defs;
        return arg_defs;
    }

    /* Private Helper Methods
    */

    // Get a description message for a new-style def hash
    function __get_description_hash(var raw_defs, var sb)
    {
        int max = 0;
        var fixed_defs = {};
        for (string arg in raw_defs) {
            var parts = split("=", arg);
            string arg_name = parts[0];
            string prefix = (length(arg_name) == 1) ? "-" : "--";
            arg_name = prefix + arg;
            fixed_defs[arg_name] = raw_defs[arg];

            int len = length(arg_name);
            if (len > max)
                max = len;
        }
        max += 4;

        for (string arg in fixed_defs) {
            string desc = fixed_defs[arg];
            push(sb, "    ");

            // TODO: add dashes. -X and --xxx instead of X and xxx
            push(sb, arg);
            push(sb, repeat_string(" ", max - length(arg)));
            push(sb, desc);
            push(sb, "\n");
        }
        return sb;
    }

    // Get a description message for an old-style array of arrays
    function __get_description_array(var raw_defs, var sb)
    {
        // TODO
    }

    /* We can handle defs of the following types:
        foo         f
        foo=s       f=s
        foo=[]      f=[]
        foo={}      f={}
    */
    // Get a processed definition hash for a new-style hash
    function __prepare_arg_defs_hash(var defs, var arg_defs)
    {
        for (string arg_def in defs) {
            var parts = split("=", arg_def);

            // Get the name of the argument
            string raw_arg_name = parts[0];
            string arg_name;
            if (length(raw_arg_name) == 1)
                arg_name = "-" + raw_arg_name;
            else
                arg_name = "--" + raw_arg_name;

            // Now get a flag that tells us how to work with it.
            string flag = "f";
            if (elements(parts) == 2) {
                string second_part = parts[1];
                switch(second_part) {
                    case "s":
                    case "[]":
                    case "{}":
                        flag = parts[1];
                        break;
                    default:
                        Rosella.Error.error("Unknown argument type for '%s': '%s'", raw_arg_name, second_part);
                }
            }

            arg_defs[arg_name] = flag;
        }
    }

    // Get a processed definition hash for an old-style array of arrays
    function __prepare_arg_defs_array(var defs, var arg_defs)
    {
        // TODO
    }
}
