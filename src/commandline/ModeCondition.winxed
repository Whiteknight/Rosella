class Rosella.CommandLine.ModeCondition
{
    function is_satisfied(var args)
    {
        return false;
    }

    function get_description(var sb)
    {
    }
}

class Rosella.CommandLine.ModeCondition.FlagSet : Rosella.CommandLine.ModeCondition
{
    var flagname;

    function FlagSet(string flagname)
    {
        self.flagname = flagname;
    }

    function is_satisfied(var args)
    {
        string flagname = string(self.flagname);
        int value = args[flagname];
        return value;
    }

    function get_description(var sb)
    {
        string flagname = self.flagname;
        push(sb, " ");
        push(sb, flagname);
    }
}

class Rosella.CommandLine.ModeCondition.ScalarValue : Rosella.CommandLine.ModeCondition
{
    var name;
    var value;

    function ScalarValue(string name, string value)
    {
        self.name = name;
        self.value = value;
    }

    function is_satisfied(var args)
    {
        string name = self.name;
        string expectedvalue = self.value;
        string actualvalue = args[name];
        if (expectedvalue == actualvalue)
            return true;
        return false;
    }

    function get_description(var sb)
    {
        string name = self.name;
        string value = self.value;
        push(sb, " ");
        push(sb, name);
        push(sb, "=");
        push(sb, value);
    }
}

class Rosella.CommandLine.ModeCondition.GenericFunc : Rosella.CommandLine.ModeCondition
{
    var func;
    var desc;

    function GenericFunc(var f, string desc)
    {
        self.func = f;
        self.desc = desc;
    }

    function is_satisfied(var args)
    {
        var f = self.func;
        return f(args);
    }

    function get_description(var sb)
    {
        string desc = self.desc;
        push(sb, " ");
        push(sb, desc);
    }
}
