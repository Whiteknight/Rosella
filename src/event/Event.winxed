namespace Rosella.Event
{
    const string DEFAULT_SUBSCRIBER_FACTORY = "Rosella.Event.default_subscriber_factory";
    function default_subscriber_factory()
    {
        var d = new Rosella.Event.Subscriber.Factory();
        while(1)
            yield(d);
    }
}

/* Event Class.
    An Event is basically a data object that holds information from the
    publisher to transmit to various subscribers. When an event is raised,
    the event and all its data are passed to Queues and, ultimately, to
    subscribers.

    See the Queue and Manager class documentation for more details.
*/
class Rosella.Event
{
    var subscribers;
    var subscriber_factory;
    var payloads;

    // Constructor
    function Event()
    {
        self.subscribers = {};
        self.subscriber_factory = Rosella.Event.default_subscriber_factory();
    }

    function subscribe(string name, var action, string dispatch)
    {
        var subscriber = self.subscriber_factory.create(action, dispatch);
        self.subscribers[name] = subscriber;
    }

    function unsubscribe(event_name)
    {
        if (exists self.subscribers[event_name])
            delete self.subscribers[event_name];
    }

    /* Event Raise Routines
    */

    function get_payload(string name, var p, var n)
    {
        return new Rosella.Event.Payload(self, name, p, n);
    }

    function publish(var pos [slurpy], var named [slurpy,named])
    {
        var payloads = {};
        for (string subscriber_name in self.subscribers) {
            var subscriber = self.subscribers[subscriber_name];
            if (subscriber != null) {
                var payload = self.get_payload(subscriber_name, pos, named);
                payloads[subscriber_name] = payload;
                subscriber.execute(payload);
            }
        }
        return payloads;
    }
}
