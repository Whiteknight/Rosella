class Rosella.Net.Http.Message.Factory : Rosella.ObjectFactory
{
    var header_type;
    var message_types;

    function Factory(var header_type = class Rosella.Net.Http.Header)
    {
        self.message_types = {
            "request" : class Rosella.Net.Http.Request,
            "response" : class Rosella.Net.Http.Response
        };
        self.header_type = header_type;
    }

    function create_request()
    {
        return self.create_typed(self.message_types["request"], self.header_type);
    }

    function create_response()
    {
        return self.create_typed(self.message_types["response"], self.header_type);
    }

    function create(var header_type = null, var header_args [slurpy,named])
    {
        if (header_type == null)
            header_type = self.header_type;
        return self.create_typed(self.target_type, header_type, header_args:[flat,named]);
    }

    function create_typed(var type, var header_type, var header_args [slurpy,named])
    {
        var header = Rosella.construct(header_type, header_args:[flat,named]);
        return Rosella.construct(type, header);
    }
}

class Rosella.Net.Http.Message
{
    var header;
    var content;
    var protocol;

    function Message(var header)
    {
        self.header = header;
    }

    function header()
    {
        return self.header;
    }

    function add_header(string name, string value)
    {
        self.header.add_value(name, value);
    }

    function get_header(string name)
    {
        return self.header.get_value(name);
    }

    function remove_header(string name)
    {
        self.header.remove_value(name);
    }

    function set_content(string content)
    {
        self.content = content;
    }

    function add_content(string content)
    {
        string existing = string(self.content);
        self.content = existing + content;
    }

    function get_content()
    {
        if (self.content == null)
            return "";
        return string(self.content);
    }

    function get_content_length_header()
    {
        string content_length = self.header.get_header("Content-Length");
        if (content_length == null || content_length == "")
            return 0;
        return int(content_length);
    }
}
