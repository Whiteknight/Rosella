namespace Rosella.Net.Http.Response
{
    function __error_bad_protocol(string protocol, string expected)
    {
        string msg = Rosella.String.sprintf("Bad protocol. Expected '%s' but have '%s'", expected, protocol);
        return new Rosella.Net.Http.Response(RC_INTERNAL_SERVER_ERROR, msg);
    }

    function __error_not_found(string path)
    {
        string msg = Rosella.String.sprintf("Could not find resource '%s'", path);
        return new Rosella.Net.Http.Response(RC_NOT_FOUND, msg);
    }

    function __error_network_failure(var request)
    {
        return new Rosella.Net.Http.Response(RC_INTERNAL_SERVER_ERROR, "Network error");
    }

    function __error_receive_failure(var request, var response)
    {
        return new Rosella.Net.Http.Response(RC_INTERNAL_SERVER_ERROR, "Error during data receive");
    }
}

class Rosella.Net.Http.Response : Rosella.Net.Response
{
    var header;
    var content;
    var code;
    var message;
    var previous;
    var request;

    function Response(int code, string message = "", var previous = null, var request = null)
    {
        self.code = code;
        self.message = message;
        self.previous = previous;
        self.request = request;
        self.header = new Rosella.Net.Http.Header();
    }

    function header() { return self.header; }
    function get_header(string name) { return self.header.get_value(name); }
    function add_header(string name, string value) { self.header.add_value(name, value); }

    function get_content()
    {
        if (self.content == null)
            return "";
        return string(self.content);
    }

    function set_content(string content)
    {
        self.content = content;
    }

    function status_code()
    {
        return int(self.code);
    }

    function get_content_length_header()
    {
        string content_length = self.header.get_value("Content-Length");
        if (content_length == null || content_length == "")
            return 0;
        return int(content_length);
    }
}
