/* URI Factory
    This factory reads the protocol from the URI and creates a suitable URI
    parser for it.
*/
class Rosella.Net.Uri.Factory : Rosella.ObjectFactory
{
    var uri_types;      // Known types of URIs
    var default_type;   // The default URI class, to use for unrecognized protocols

    // Constructor
    function Factory()
    {
        self.uri_types = {};
        self.default_type = class Rosella.Net.Uri;
    }

    // Set the type of URI parser object to use for the given protocol
    function add_uri_type(string protocol, var type)
    {
        self.uri_types[protocol] = type;
    }

    // Create a URI object for the given URI string
    function create(string uristr)
    {
        :(uristr, var s, var b, int len) = Rosella.Parse.setup_parse(uristr);
        string protocol = Rosella.Parse.parse_alphanumeric(uristr, s, b, len);
        protocol = string_to_lower(protocol);
        var uri_type = null;
        if (exists self.uri_types[protocol])
            uri_type = self.uri_types[protocol];
        else
            uri_type = self.default_type;
        if (uri_type == null)
            Rosella.Error.error("Cannot create Uri for protocol type '%s'", protocol);

        var uri = Rosella.construct(uri_type, protocol, uristr);
        uri.parse(uristr, s, b, len);
        return uri;
    }

    // Create a URI object of the given type with the given URI string.
    // This is probably not what you want, unless you know exactly what you are
    // doing
    function create_typed(var type, string uristr)
    {
        int s[] = [];
        var b = get_iterator(uri);
        int len = length(uri);
        string protocol = Rosella.Parse.parse_alphanumeric(uri, s, b, len);

        var uri = Rosella.construct(type, uristr);
        uri.parse(protocol, uristr, s, b, len);
        return uri;
    }

}
