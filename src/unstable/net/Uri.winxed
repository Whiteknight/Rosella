namespace Rosella.Net.Uri
{
    const string URI_COMP_UNCHANGE = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789-_.~";
    const string URI_UNCHANGE      = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789-_.~!*\'();:@&=+$,/?#[]";

    function percent_encode_except(string s, string except)
    {
        int s_enc = encoding(s);
        int utf8_enc = find_encoding("utf8");
        if (s_enc != find_encoding("ascii") && s_enc != utf8_enc)
            s = trans_encoding(s, utf8_enc);

        var buf = new 'ByteBuffer';
        var result = new 'ByteBuffer';
        buf =: s;
        for (int code in buf) {
            string c = chr(code);
            int idx = index(except, c);
            if (idx == -1) {
                push(result, 0x25);
                c = substr(HEXDIGITS, code / 16, 1);
                push(result, ord(c));
                c = substr(HEXDIGITS, code % 16, 1);
                push(result, ord(c));
            } else {
                push(result, code);
                continue;
            }
        }
        s = result.get_string("ascii");
        return s;
    }

    function percent_encode(string s)
    {
        return percent_encode_except(s, URI_UNCHANGE);
    }

    function percent_encode_component(string s)
    {
        return percent_encode_except(s, URI_COMP_UNCHANGE);
    }
}

class Rosella.Net.Uri
{
    var parts;

    function Uri(string uri)
    {
        self.parts = self.__parse(uri);
    }

    function __parse(string uri)
    {
        var parts = {
            "URI" : uri
        };
        int idx = index(uri, "//");
        string protocol = (idx > 2) ? substr(uri, 0, idx - 1) : "";
        parts["Protocol"] = protocol;
        uri = substr(uri, idx + 2);
        parts["Path"] = uri;
        idx = index(uri, "/");
        parts["Path Query"] = (idx >= 0) ? substr(uri, idx) : "";
        string port_authority = substr(uri, 0, idx);
        idx = index(uri, ":");
        if (idx < 0) {
            parts["Authority"] = uri;
            //parts["Port"] = "";
            switch(protocol) {
                case "http":
                    parts["Port"] = "80";
                    break;
                default:
                    parts["Port"] = "";
                    break;
            }
        } else {
            parts["Authority"] = substr(uri, 0, idx);
            parts["Port"] = substr(uri, idx + 1);
        }
        return parts;
    }

    function host() { return self.authority(); }

    function uri() { return self.parts["URI"]; }

    function protocol() { return self.parts["Protocol"]; }

    function path() { return self.parts["Path"]; }

    function authority() { return self.parts["Authority"]; }

    function port() { return self.parts["Port"]; }

    function path_query() { return self.parts["Path Query"]; }
}
