namespace Rosella { namespace Dumper { namespace DumpHandler
{
    class Object : Rosella.Dumper.DumpHandler
    {
        function Object() { }

        function can_dump(var obj)
        {
            var class_obj = typeof(obj);
            int isa_class = isa_s(class_obj, "PMCProxy");
            return isa_class == 0 ? true : false;
        }

        function dump(var dump, var emit, var obj)
        {
            var class_obj = typeof(obj);
            emit.text("'%s' ", class_obj);
            emit.object(function() {
                for (string key in class_obj.attributes()) {
                    emit.text_line("%s => ", key);
                    dump.__dump_recurse(emit, obj.*key);
                }
            });
        }
    }
}}}
