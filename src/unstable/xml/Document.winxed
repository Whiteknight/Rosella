class Rosella.Xml.Document
{
    var xml_header;
    var dtd_header;
    var root;

    function Document() { }

    function add_header(var header)
    {
        if (header instanceof Rosella.Xml.Tag.XmlHeader) {
            if (self.xml_header != null)
                Rosella.Error.error("Document already contains an XML Header");
            self.xml_header = header;
        } else if (header instanceof Rosella.Xml.Tag.DtdHeader) {
            if (self.dtd_header != null)
                Rosella.Error.error("Document already contains a DTD Header");
            self.dtd_header = header;
        }
    }

    function set_documentroot(var root)
    {
        for (var child in root.children)
        {
            if (child instanceof Rosella.Xml.Tag) {
                self.set_root(child);
                return;
            }
        }
    }

    function set_root(var tag)
    {
        if (self.root != null)
            Rosella.Error.error("document already has root tag");
        self.root = tag;
    }

    function get_document_root()
    {
        return self.root;
    }

    function to_xml()
    {
        return self.get_document_root().to_xml();
    }

    function write_to_file(string filename)
    {
        string xml = self.to_xml();
        (new Rosella.FileSystem.File(filename)).write_all_text(xml);
    }

    function read_from_file(string filename)
    {
        var f = new Rosella.FileSystem.File(filename);
        string xml = f.read_all_text();
        Rosella.Xml.parse(xml, self);
    }

    function read_from_string(string xml)
    {
        Rosella.Xml.parse(xml, self);
    }
}
