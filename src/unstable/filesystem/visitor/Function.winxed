namespace Rosella { namespace FileSystem { namespace Visitor
{
    /* Function Visitor
        This visitor class executes an arbitrary function reference on all
        entries.
    */
    class Function : Rosella.FileSystem.Visitor
    {
        var func;
        var values;

        // Constructor
        function Function(var func,
                int dofiles [named,optional], int has_f [opt_flag],
                int dodirs [named,optional], int has_d [opt_flag])
        {
            if (!has_f)
                dofiles = 1;
            if (!has_d)
                dodirs = 0;
            self.Visitor(dofiles, dodirs);
            self.func = func;
            self.values = [];
        }

        // Run the function on the entry. Save the return values, if any
        // in a growing list of results
        function process(string entry)
        {
            var func = self.func;
            var args;
            :(args [slurpy]) = func(entry);
            for (var arg in args)
                push(self.values, arg);
        }

        // Return the lits of results, if any
        function result()
        {
            return self.values;
        }
    }
}}}
