const string TEMPLATE_FILE_FMT = "%s.%s.template";

function main[main](var args)
{
    load_bytecode("rosella/template.pbc");
    load_bytecode("rosella/filesystem.pbc");

    if (elements(args) < 3) {
        show_help();
        exit(0);
    }
    string programname = args.shift();
    string mode = args.shift();
    string language = args.shift();

    using Rosella.Parrot.try_report;
    try_report(function() {
        template_main(mode, language, args);
    });
}

function template_main(string mode, string language, var args)
{
    string template;
    var context;
    switch (mode) {
        case "harness":
            template = get_template_text("test_harness", language);
            context = harness_template_context(template, args);
            break;
        case "test":
            template = get_template_text("test", language);
            context = test_template_context(language, args[0], args[1]);
            break;
        default:
            show_help();
    }
    var engine = new Rosella.Template.Engine();
    string output = engine.generate(template, context);
    say(output);
}

function get_template_text(string type, string lang)
{
    using Rosella.Template.get_standard_template_file;
    string filename = sprintf(TEMPLATE_FILE_FMT, [type, lang]);
    filename = get_standard_template_file(filename);
    var file = new Rosella.FileSystem.File(filename);
    if (!file.exists()) {
        say(sprintf("Cannot find %s template for language '%s'", [type, lang]));
        show_help();
        exit(0);
    }
    return file.read_all_text();
}


function harness_template_context(string template, var dirs)
{
    var context = {
        "test" : {
            "dirs" : dirs
        }
    };
    return context;
}

function test_template_context(string template, string libname, string classname)
{
    load_bytecode(libname);
    using Rosella.get_type_class;
    using Rosella.String.replace_all;

    var targetclass = get_type_class(split(".", classname));
    var methods = targetclass.methods();

    var context = {
        "class" : {
            "name" : classname,
            "cleanname" : replace_all(classname, ".", "_"),
            "winxedname" : classname,
            "perlname" : replace_all(classname, ".", "::"),
            "methods" : methods
        }
    };
    return context;
}

function show_help()
{
    string text = <<:
winxed test_template.winxed harness <lang> <dirs...>
    Create a test harness

winxed test_template.winxed test <lang> <.pbc_file> <class_name>
    Creates a stub test file for an existing library class

Options:
    <lang> one of "nqp" or "winxed"
:>>
;
    say(text);
}
