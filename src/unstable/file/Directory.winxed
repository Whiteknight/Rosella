$include_const "src/unstable/file/Constants.pasm";

namespace Rosella { namespace File
{
    namespace Directory
    {
        function current_directory()
        {
            using Rosella.File.get_os_pmc;
            string cwd = get_os_pmc().cwd();
            return new Rosella.File.Directory(cwd);
        }

        function walk(string dirname, var visitor [optional], int has_v [opt_flag])
        {
            if (!has_v)
                visitor = new Rosella.File.Visitor.FileList(1, 1);
            private_visit_dir(dirname, "", visitor);
            return visitor.result();
        }

        // Get a list of test files from a directory. Allow recursion into
        // subdirectories, if requested.
        function private_visit_dir(string path, string name, var visitor)
        {
            using Rosella.File.is_file;
            using Rosella.File.is_directory;
            using Rosella.File.get_os_pmc;

            visitor.begin_folder(path, name);
            var contents_raw = get_os_pmc().readdir(path);

            for (string file in contents_raw) {
                string entry = path + "/" + file;

                // Don't follow the "special" files
                if (file == "." || file == "..")
                    continue;

                int isfile = is_file(entry);
                if (isfile == STAT_ISREG)
                    visitor.visit(entry);
                else {
                    int isdir = is_directory(entry);
                    if (isdir == STAT_ISDIR)
                        private_visit_dir(entry, file, visitor);
                }
            }
            visitor.end_folder(path, name);
        }
    }

    class Directory
    {
        var path;

        function Directory(string path)
        {
            self.path = path;
        }

        function get_string[vtable]()
        {
            return self.path;
        }

        function exists()
        {
            using Rosella.File.get_os_pmc;
            return get_os_pmc().exists(self.path);
        }

        function delete(int recursive [optional], int has_r [opt_flag])
        {
            // TODO
        }

        function create()
        {
            using Rosella.File.get_os_pmc;
            var os = get_os_pmc();
            if (!os.exists(self.path))
                // 493 = 0755 octal.
                os.mkdir(self.path, 493);
        }

        function rename(string new_path)
        {
            using Rosella.File.get_os_pmc;
            get_os_pmc().rename(self.path, new_path);
            self.path = new_path;
        }

        function get_files()
        {
            using Rosella.File.get_os_pmc;
            string path = string(self.path) + "/";
            var file_names = get_os_pmc().readdir(path);
            var files = [];
            using Rosella.File.is_file;
            for (string file_name in file_names) {
                if (file_name == "." || file_name == "..")
                    continue;
                if (is_file(path + file_name))
                    push(files, new Rosella.File.File(file_name));
            }
            return files;
        }

        function get_subdirectories()
        {
            using Rosella.File.get_os_pmc;
            string path = string(self.path) + "/";
            var names = get_os_pmc().readdir(path);
            var dirs = [];
            using Rosella.File.is_directory;
            for (string name in names) {
                if (name == "." || name == "..")
                    continue;
                if (is_directory(path + name))
                    push(dirs, new Rosella.File.Directory(name));
            }
            return dirs;
        }
    }
}}
