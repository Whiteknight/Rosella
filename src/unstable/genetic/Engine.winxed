class Rosella.Genetic.Engine
{
    var cells;
    var mutator;
    var evaluator;
    var counts;

    const int COUNT_TOTAL_CELLS = 0;
    const int COUNT_NUM_WINNERS = 1;
    const int COUNT_FITNESS_THRESHOLD = 2;

    function Engine(var counts, var mutator, var evaluator)
    {
        self.counts = counts;
        int num_cells = counts[COUNT_TOTAL_CELLS];
        var cells = [];
        mutator.create_initial_cells(cells, num_cells);
        self.cells = cells;
        //say(self.cells);
        self.mutator = mutator;
        self.evaluator = evaluator;
    }

    function run(int num_generations)
    {
        var winners = self.run_one_generation();
        for (int i = 1; i < num_generations; i++) {
            self.get_next_generation(winners);
            winners = self.run_one_generation();
        }
        return Rosella.Query.as_queryable(winners)
            .map(function(cell) { return cell.data(); });
    }

    function run_one_generation()
    {
        int threshold = self.counts[COUNT_FITNESS_THRESHOLD];
        int num_winners = self.counts[COUNT_NUM_WINNERS];
        var fitness = self.evaluator;
        var compare_key = Rosella.Query.get_default_comparer();
        var winners = Rosella.Query.as_queryable(self.cells)
            .map(function(cell) {
                int f = int(fitness(cell.data()));
                //Rosella.IO.sayf("Mapping fitness: %f -> %d", cell.data().value, f);
                return [f, cell];
            })
            .filter(function(cell) { return int(cell[0]) > threshold; })
            //.foreach(function(x) { Rosella.IO.sayf("Unsorted: %d", x[0]); })
            .sort(function(x, y) { return compare_key(x[0], y[0]); })
            //.foreach(function(x) { Rosella.IO.sayf("Sorted: %d", x[0]); })
            .take(num_winners)
            //.foreach(function(x) { Rosella.IO.sayf("Top X: %d", x[0]); })
            .map(function(x) { return x[1]; })
            .data();
        return winners;
    }

    function get_next_generation(var winners)
    {
        int num_cells = self.counts[COUNT_TOTAL_CELLS];
        self.cells = self.mutator.get_next_generation(winners, num_cells);
    }
}
