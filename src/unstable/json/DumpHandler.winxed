class Rosella.Json.DumpHandler.Array : Rosella.Dumper.DumpHandler.Array
{
    // Dump the array
    function dump(var dump, var emit, var obj)
    {
        emit.array(function() {
            for (int i = 0; i < elements(obj); i++) {
                dump.__dump_recurse(emit, obj[i]);
                if (i != elements(obj) - 1)
                    emit.text(", ");
            }
        });
    }
}

class Rosella.Json.DumpHandler.Hash : Rosella.Dumper.DumpHandler.Hash
{
    function dump(var dump, var emit, var obj)
    {
        emit.hash(function() {
            var i = get_iterator(obj);
            if (!i)
                return;
            string key = shift_string(i);
            emit.text_line("'%s' : ", key);
            dump.__dump_recurse(emit, obj[key]);
            while (i) {
                emit.text(",");
                key = shift_string(i);
                emit.text_line("'%s' : ", key);
                dump.__dump_recurse(emit, obj[key]);
            }
        });
    }
}

class Rosella.Json.DumpHandler.Object : Rosella.Dumper.DumpHandler.Object
{
    function dump(var dump, var emit, var obj)
    {
        emit.hash(function() {
            var seen_attrs = {};
            var class_obj = typeof(obj);
            for (var parent in class_obj.inspect("all_parents")) {
                for (string key in parent.attributes()) {
                    if (!(exists seen_attrs[key])) {
                        seen_attrs[key] = true;
                    }
                }
            }

            var i = get_iterator(seen_attrs);
            if (!i)
                return;
            string key = shift_string(i);
            emit.text_line("'%s' : ", key);
            dump.__dump_recurse(emit, obj.*key);
            while (i) {
                emit.text(", ");
                key = shift_string(i);
                emit.text_line("'%s' : ", key);
                dump.__dump_recurse(emit, obj.*key);
            }
        });
    }
}

class Rosella.Json.DumpHandler.Null : Rosella.Dumper.DumpHandler
{
    function can_dump(var obj) { return obj == null; }

    function dump(var dump, var emit, var obj) { emit.text("null"); }
}

class Rosella.Json.DumpHandler.Integer : Rosella.Dumper.DumpHandler
{
    function can_dump(var obj) { return true; }

    function dump(var dump, var emit, var obj) { emit.text("%d", int(obj)); }
}

class Rosella.Json.DumpHandler.Float : Rosella.Dumper.DumpHandler
{
    function can_dump(var obj) { return true; }

    function dump(var dump, var emit, var obj) { emit.text("%f", float(obj)); }
}

class Rosella.Json.DumpHandler.String : Rosella.Dumper.DumpHandler
{
    function can_dump(var obj) { return true; }

    function dump(var dump, var emit, var obj) { emit.text("'%s'", obj); }
}

class Rosella.Json.DumpHandler.Boolean : Rosella.Dumper.DumpHandler
{
    function can_dump(var obj) { return true; }

    function dump(var dump, var emit, var obj) { emit.text(obj ? "true" : "false"); }
}
