namespace Rosella { namespace Template { namespace Handler
{
    /* Sub-Template including
        Include template text from another file.
    */
    class Include : Rosella.Template.Handler.Childless
    {
        var filename;

        // Constructor
        function Include(var tokens)
        {
            if (elements(tokens) != 1)
                Rosella.Error.invalid(__FUNCTION__, "Include requires exactly 1 argument");
            self.filename = tokens[0];
        }

        // Get template text from the file and pass it to the template
        // engine
        function render(var engine, var ctx, var sb)
        {
            var file = new Rosella.FileSystem.File(self.filename);
            if (!file.exists())
                Rosella.Error.invalid(__FUNCTION__, "File '%s' does not exist", self.filename);
            string text = file.read_all_text();
            string output = engine.generate(text, ctx);
            push(sb, output);
        }
    }
}}}
