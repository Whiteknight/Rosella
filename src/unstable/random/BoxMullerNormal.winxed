class Rosella.Random.BoxMullerNormal : Rosella.Random.RandomNumber
{
    var uniform_generator;
    var spare;
    var center;
    var stddev;

    function BoxMullerNormal(float center, float stddev)
    {
        self.uniform_generator = Rosella.Random.default_uniform_random();
        self.center = center;
        self.stddev = stddev;
    }

    function initialize(int seed)
    {
        self.uniform_generator.initialize(seed);
    }

    function get_int()
    {
        return int(self.__next(float(self.center), float(self.stddev)));
    }

    function get_float()
    {
        return self.__get_next(float(self.center), float(self.stddev));
    }

    function __get_next(float center, float stddev)
    {
        if (self.spare != null)
        {
            float spare = float(self.spare);
            self.spare = null;
            return spare;
        }

        float x, y, r;
        do {
            x = self.uniform_generator.get_float() * 2.0 - 1;
            y = self.uniform_generator.get_float() * 2.0 - 1;
            r = x * x + y * y;
        } while(r == 0.0 || r > 1.0);
        float z = Rosella.Random.__sqrt(-2.0 * Rosella.Random.__ln(r) / r);
        self.spare = center + stddev * y * z;
        return center + stddev * x * z;
    }
}
