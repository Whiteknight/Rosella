namespace Rosella
{
    /* The Rosella Test Library
        This library is used to implement unit tests, although not a test
        harness. By default, the test library produces TAP output, although
        that and much other behavior can be overridden by subclassing
    */
    namespace Test
    {
        // Setup and run a test using Testcase-based tests.
        function test(var testproto,
                var loaderproto [named,optional], int has_lp [opt_flag], // the type of Loader to use
                var loaderopts [named,optional], int has_lo [opt_flag],  // hash of options for the Loader
                var suiteproto [named,optional], int has_sp [opt_flag])  // the type of Suite to use)
        {
            using Rosella.build;
            using Rosella.get_type_name;
            using Rosella.Test.Loader.load;

            string test_type_name = get_type_name(testproto);

            // Get the Loader. If nothing else is specified, use the default
            // Rosella::Test::Loader
            if (!has_lo)
                loaderopts = {};
            if (!has_lp)
                loaderproto = class Rosella.Test.Loader;
            // TODO: Move the load logic here, and don't use a separate func.
            var tests = load(loaderproto, testproto, loaderopts);

            // Get the Suite. If nothing else is specified use the default
            // Rosella::Test::Suite
            if (!has_sp)
                suiteproto = class Rosella.Test.Suite;
            var suite = build(suiteproto, tests, "Suite for " + test_type_name);
            suite.run();
        }
    }
}
