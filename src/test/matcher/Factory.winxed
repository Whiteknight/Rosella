class Rosella.Test.Matcher.Factory : Rosella.ObjectFactory
{
    var matchers;
    var default_matcher;

    function Factory()
    {
        self.matchers = {
            "array" : new Rosella.Test.Matcher.Array(),
            "hash" : new Rosella.Test.Matcher.Hash(),
            "stringify" : new Rosella.Test.Matcher.Stringify(),
            "number" : new Rosella.Test.Matcher.Number(0.00001)
        };
        self.default_matcher = new Rosella.Test.Matcher();
    }

    function create(var a, var b)
    {
        for (string name in self.matchers) {
            if (self.matchers[name].can_match(a, b))
                return self.matchers[name];
        }
        return self.default_matcher;
    }

    function create_named(string name)
    {
        if (exists self.matchers[name])
            return self.matchers[name];
        return null;
    }

    function add_matcher(string name, var matcher)
    {
        self.matchers[name] = matcher;
    }

    function remove_matcher(string name)
    {
        if (exists self.matchers[name])
            delete self.matchers[name];
    }
}
