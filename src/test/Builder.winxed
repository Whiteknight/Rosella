namespace Rosella { namespace Test
{
    /* A TAP output builder. Can be used similarly to Test::More from the Parrot
       library.
    */
    class Builder
    {
        var test_number;
        var output;

        function Builder(var output [optional], int has_out [opt_flag])
        {
            self.test_number = 0;
            if (!has_out)
                output = getstdout();
            self.output = output;
        }

        function _printf(string fmt, var args [slurpy])
        {
            self.output.print(sprintf(fmt, args));
        }

        function reset()
        {
            self.test_number = 0;
        }

        function todo(int pass, string msg,
                string todo [optional], int has_todo [opt_flag])
        {
            self.test_number = self.test_number + 1;
            string output = self._get_result_text(self.test_number, pass, msg);
            if (!has_todo)
                todo = "";
            self._printf("%s # TODO %s\n", output, todo);
        }

        function ok(int pass, string msg [optional], int has_msg [opt_flag])
        {
            self.test_number = self.test_number + 1;
            if (!has_msg)
                msg = null;
            string result_text = self._get_result_text(self.test_number, pass, msg);
            self._printf("%s\n", result_text);
        }

        function _get_result_text(int number, int pass, string msg)
        {
            string result = null;
            if (pass)
                result = "ok " + number;
            else
                result = "not ok " + number;
            if (msg != null)
                return result + " - " + msg;
            else
                return result;
        }

        function diag(var error)
        {
            if (error instanceof "Exception") {
                var errmsg = error.message;
                var lines = split("\n", errmsg);
                for (string line in lines)
                    self._printf("# %s\n", line);

                var bt_list = error.backtrace_strings();
                for (string bt in bt_list) {
                    var frames = split("\n", bt);
                    for (string frame in frames)
                        self._printf("# %s\n", frame);
                }


            } else
                self._printf("# %s\n", string(error));
        }

        function plan(int count)
        {
            // TODO: Error checking
            self._printf("1..%d\n", count);
        }
    }
}}
