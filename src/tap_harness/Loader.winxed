namespace Rosella { namespace Harness
{
    /* Test Loader Class
        Load TestFile objects from a list of files or a list of directories.
        The exact type of TestFile object to load is selectable.
    */
    class Loader
    {
        var os;
        var testfile_type;

        /* Public API Functions
        */

        function BUILD(var testfile_type)
        {
            self.testfile_type = testfile_type;
            loadlib("os");
            self.os = new 'OS';
        }

        // Return a list of TestFile objects from a list of directories.
        // Search through those directories, possibly recursively, for a list
        // of files which appear to be tests.
        function get_tests_from_dirs(var dirs, int recurse)
        {
            var tests = [];
            for (string dir in dirs) {
                var rawfiles = [];
                self.get_dir_contents(dir, recurse, rawfiles);
                for (string filename in rawfiles) {
                    var testobj = self.make_test_obj(filename);
                    push(tests, testobj);
                }
            }
            return tests;
        }

        // Return a list of TetsFile objects from a list of files. Do not do
        // any detection or sanity checking, all files in the list are
        // assumed to be valid test files for this loader.
        function get_tests_from_files(var filenames)
        {
            var tests = [];
            for (string filename in filenames) {
                var testobj = self.make_test_obj(filename);
                push(tests, testobj);
            }
            return tests;
        }

        /* Internal Routines
        */

        // Get a list of test files from a directory. Allow recursion into
        // subdirectories, if requested.
        function get_dir_contents(string path, int recurse, var contents)
        {
            int STAT_ISREG = 0x8000;
            int STAT_ISDIR = 0x4000;
            var contents_raw = self.os.readdir(path);

            for (string file in contents_raw) {
                string entry = path + "/" + file;
                var stat = self.os.stat(entry);
                int mode = stat[2];
                int isdir = 0;
                ${ band isdir, mode, STAT_ISDIR };
                int isfile = 0;
                ${ band isfile, mode, STAT_ISREG };
                if (file[0] == ".")
                    continue;
                if (isfile == STAT_ISREG && self.is_test(entry))
                    push(contents, entry);

                if (recurse && isdir == STAT_ISDIR)
                    self.get_dir_contents(entry, recurse, contents);
            }
        }

        // Determine if the file is a valid test. Probably best to override this in
        // a subclass if you need different behaviors.
        // TODO: Maybe read the shebang preamble to make sure we have the
        //       correct type of file.
        function is_test(string filename)
        {
            if (indexof(filename, ".t") == -1)
                return 0;
            if (indexof(filename, ".OLD") != -1)
                return 0;
            return 1;
        }

        // Make a suitable TestFile object for the given file.
        function make_test_obj(string filename)
        {
            using Rosella.build;
            return build(self.testfile_type, filename);
        }
    }
}}

