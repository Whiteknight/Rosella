namespace Rosella
{
    /* An Action is a bundled command which can be packed up and executed
       later. Actions are used to prepare commands or sequences of commands
       for use in deferred execution.

       Every Action takes an object and possibly some additional arguments.
       The action is performed on the object. Argument objects can be used
       to dynamically create a list of arguments for each invocation of the
       Action.
    */
    class Action
    {
        var method;
        var args;

        /* Constructor
        */

        function Action(var method,
                var args [optional], int has_args [opt_flag])
        {
            if (method == null) {
                using Rosella.Error.invalid;
                invalid("Action", "Subroutine is null");
            }
            self.method = method;
            if (has_args) {
                using Rosella.Action.Argument.verify_all_arguments;
                verify_all_arguments("Action", args);
                self.args = args;
            } else
                self.args = [];
        }

        /* Public Functions
        */

        // Convert the list of Argument objects into an array of positional
        // arguments and a hash of named arguments. In addition to the
        // pre-defined Argument objects, take an optional list of Argument
        // overrides.
        function prepare_args(var pos, var named,
                var overrides [optional], int has_overrides [opt_flag])
        {
            for (var arg in self.args)
                arg.resolve_to(pos, named);
            if (has_overrides) {
                using Rosella.Action.Argument.verify_all_arguments;
                verify_all_arguments("Action.prepare_args", overrides);
                for (var override in overrides)
                    override.resolve_to(pos, named);
            }
        }

        // Execute the action on the given object. Take an optional list of
        // Argument overrides to change the default behavior.
        function execute(var obj,
                var overrides [optional], int has_overrides [opt_flag])
        {
            var pos = [];
            var named = {};
            if (!has_overrides)
                overrides = [];
            self.prepare_args(pos, named, overrides);
            self.execute_initializer(obj, pos, named);
        }

        // Perform the actual action. This must be done in a subclass.
        function execute_initializer(var obj, var pos, var named)
        {
            using Rosella.Error.must_subclass;
            must_subclass("Rosella.Action");
        }
    }
}
