namespace Rosella { namespace Harness
{
    class FileResult
    {
        var file;
        var errdetails;
        var failures;
        var todo_passed;
        var num_tests;
        var passed_tests;
        var status;

        function FileResult(var file)
        {
            self.file = file;
            file.set_result(self);
            self.failures     = [];
            self.todo_passed  = [];
            self.passed_tests = [];
            self.errdetails = null;
            self.status = null;
        }

        /* Public Accessors
        */

        function total_tests() { return self.num_tests; }

        function passed_tests() { return self.passed_tests; }

        function list_of_failures() { return self.failures; }

        function list_of_todo_passed() { return self.todo_passed; }

        function status()
        {
            if (self.status != null)
                return self.status;
            if (elements(self.failures) > 0)
                return "FAILED";
            return "PASSED";
        }

        function error_details() { return self.errdetails; }

        function get_string[vtable]() { return string(self.file); }

        /* Helper and Status routines
        */

        function mark_test_abort(var err)
        {
            self.errdetails = err;
            self.status = "ABORTED";
        }

        function mark_test_empty()
        {
            self.status = "EMPTY";
        }

        function add_pass(string name, int num, int todo)
        {
            string msg = Rosella.String.sprintf("test %d: %s", num, name);
            if (todo == 1)
                push(self.todo_passed, msg);
            else
                push(self.passed_tests, msg);
        }

        function add_fail(string name, int num, int todo)
        {
            string msg = Rosella.String.sprintf("test %d: %s", num, name);
            if (todo == 1)
                push(self.passed_tests, msg);
            else
                push(self.failures, msg);
        }

        function set_plan(int num)
        {
            // TODO: Check bounds. Make sure we run this many tests
            self.num_tests = num;
        }
    }
}}
