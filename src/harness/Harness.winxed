namespace Rosella
{
    /* TAP Harness class. Contains a list of TestFile objects and executes
       each in a loop. The Harness keeps statistics about the test run, and
       passes them off to other routines for display and analysis.

       Harness is the Controller object in the MVC system. Harness::View is
       the view which displays to the user, and Harness::Results is the model.
    */
    class Harness
    {
        /* Routines for Running the Harness
        */

        function default_view()
        {
            return new Rosella.Harness.View;
        }

        // Run the harness. Optionally take a custom view, and a handful of
        // other named options.
        function run(var test_run, var view,
                int run_inline [named,optional],  int has_ri [opt_flag],
                int line_length [named,optional], int has_ll [opt_flag],
                int debug [named,optional], int has_dbg [opt_flag])
        {
            if (!has_ri)
                run_inline = 0;
            if (!has_ll)
                line_length = 0;
            if (!has_dbg)
                debug = 0;
            return self.run_internal(test_run, view, run_inline, debug);
        }

        // Interal routine to run the harness
        function run_internal(var test_run, var view, int run_inline, int debug)
        {
            for (var test in test_run.tests()) {
                view.start_test(test);
                test.run(view, debug, run_inline);
                test_run.update_test_stats(test);
                view.end_test(test);
            }
            test_run.mark_completed();
            view.test_run_complete(test_run);
            return test_run.run_was_success();
        }
    }
}
