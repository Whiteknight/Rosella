namespace Rosella { namespace Harness { namespace TestFile
{
    /* TestFile Factory Class
        Load TestFile objects from a list of files or a list of directories.
        The exact type of TestFile object to load is selectable.
    */
    class Factory : Rosella.ObjectFactory
    {
        var os;

        /* Public API Functions
        */

        function Factory(var testfile_type)
        {
            self.ObjectFactory(testfile_type);
            self.os = Rosella.FileSystem.get_os_pmc();
        }

        // Make a suitable TestFile object for the given file.
        function create_typed(var type, string filename)
        {
            using Rosella.construct;
            return construct(type, filename);
        }

        // Return a list of TestFile objects from a list of directories.
        // Search through those directories, possibly recursively, for a list
        // of files which appear to be tests.
        function create_tests_from_dirs(var test_type, var dirs, int recurse)
        {
            var tests = [];
            for (string dirpath in dirs) {
                var dir = new Rosella.FileSystem.Directory(dirpath);
                var rawfiles;
                if (recurse) {
                    rawfiles = dir.walk_func(function (var file) {
                        var path = file.path_obj();
                        if (path.extension() != "t")
                            return;
                        return file;
                    });
                } else {
                    var allfiles = dir.get_files();
                    rawfiles = [];
                    for (var file in allfiles) {
                        var path = file.path_obj();
                        if (path.extension() == "t")
                            push(rawfiles, file);
                    }
                }
                for (var file in rawfiles) {
                    var testobj = self.create_typed(test_type, string(file));
                    push(tests, testobj);
                }
            }
            return tests;
        }


        // Return a list of TestFile objects from a list of files. Do not do
        // any detection or sanity checking, all files in the list are
        // assumed to be valid test files for this loader.
        function create_tests_from_files(var test_type, var filenames)
        {
            var tests = [];
            for (string filename in filenames) {
                var file = new Rosella.FileSystem.File(filename);
                if (!file.exists()) {
                    using Rosella.Error.invalid;
                    invalid(__FUNCTION__, "file '%s' does not exist", filename);
                }
                var testobj = self.create_typed(test_type, filename);
                push(tests, testobj);
            }
            return tests;
        }

        // Determine if the file is a valid test. Probably best to override this in
        // a subclass if you need different behaviors.
        // TODO: Maybe read the shebang preamble to make sure we have the
        //       correct type of file.

    }
}}}

