namespace Rosella { namespace MockObject
{
    /* Mock Object Factory.
        Uses the Proxy factory to create and configure mock object
        controllers.
    */
    class Factory : Rosella.ObjectFactory
    {
        var proxy_factories;     // A hash of proxy factories, by target type name key
        var controller_factory;  // A MockObject.Controller factory

        function BUILD()
        {
            using Rosella.build;

            self.proxy_factories = {};
            var c_type = class Rosella.MockObject.Controller;
            self.controller_factory = build(class Rosella.ObjectFactory, c_type);
        }

        // Get a new Mock and Controller. Return the Controller, from which
        // we can get the mock later
        function create_typed(var type_to_mock, var opts [slurpy,named])
        {
            var controller = self.controller_factory.create();
            var mock_factory = self.get_mock_factory_internal(type_to_mock, opts);
            controller.mock = mock_factory.create(controller);
            return controller;
        }

        // Get a new proxy factory for the given type. For internal use only.
        function get_mock_factory_internal(var type_to_mock, var opts)
        {
            using Rosella.build;
            using Rosella.get_type_name;

            string type_name = get_type_name(type_to_mock);
            if (exists self.proxy_factories[type_name])
                return self.proxy_factories[type_name];

            var builders = self.get_builder_types();

            var factory = build(class Rosella.Proxy.Factory, type_to_mock, builders);
            self.proxy_factories[type_name] = factory;
            return factory;
        }

        function get_builder_types()
        {
            using Rosella.Proxy.Builder.get_builders;
            var builder_types = [
                "MethodIntercept",
                "AttributeIntercept",
                "InvokeIntercept"
            ];
            return get_builders(builder_types);
        }
    }
}}
