namespace Rosella { namespace Decorator
{
    class Factory : Rosella.ObjectFactory
    {
        var proxy_factory;
        var methods;
        var attributes;

        function BUILD(var target_type, var methods, var attrs,
            int is_immutable [optional,named], int has_is_i [opt_flag])
        {
            using Rosella.build;
            self.methods = methods;
            self.attributes = attrs;
            int immutable = 0;
            if (has_is_i && is_immutable)
                immutable = 1;
            var builders = [
                build(class Rosella.Decorator.Builder, immutable)
            ];
            self.proxy_factory = build(class Rosella.Proxy.Factory, target_type, builders);
        }

        function create(var target, var p [slurpy], var n [slurpy,named])
        {
            using Rosella.Proxy.set_proxy_private_attr;

            var proxy = self.proxy_factory.create(null);
            set_proxy_private_attr(proxy, "methods", self.methods);
            set_proxy_private_attr(proxy, "attributes", self.attributes);
            set_proxy_private_attr(proxy, "target", target);
            return proxy;
        }

        function create_typed(var type, var p [slurpy], var n [slurpy,named])
        {
            die("Cannot create_typed with a Decorator.Factory. Create a new factory");
        }
    }
}}
