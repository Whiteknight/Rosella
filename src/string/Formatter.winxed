/* String Formatting Engine
    This class implements a variant of .NET-style String.Format formatting. It
    is like an alternate syntax/semantic for sprintf.
*/
class Rosella.String.Formatter
{
    var type_converters;
    var role_converters;
    var default_converter;

    // Constructor
    function Formatter()
    {
        self.type_converters = Rosella.get_pmc_keyed_hash();
        self.role_converters = {};
        self.default_converter = new Rosella.String.Converter.Sprintf();
    }

    // Add a converter for a specific type
    function add_type_converter(var type, var converter)
    {
        var type_c = Rosella.get_type_class(type);
        self.converters[type_c] = converter;
    }

    // Add a converter for a role
    function add_role_converter(string role, var converter)
    {
        self.role_converters[role] = converter;
    }

    // Format the string
    function format(string fmt, var args [slurpy])
    {
        int cclass_numeric = Rosella.String.get_character_class("numeric");
        string raw_args[] = [];
        var cache = {};

        string output_array[] = [];
        int fmt_len = length(fmt);
        int out_idx = 0;
        int cur_idx = 0;
        int next_idx = 0;

        while(cur_idx < fmt_len) {
            cur_idx = next_idx;
            next_idx = index_ssi(fmt, "{", cur_idx);
            if (cur_idx != next_idx)
                output_array[out_idx++] = substr(fmt, cur_idx, next_idx - cur_idx);

            if (next_idx == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            // Else, we've found a format sequence.
            next_idx++;
            if (fmt[next_idx] == "{") {
                output_array[out_idx++] = "{";
                continue;
            }
            int ptr = index_ssi(fmt, "}", next_idx);
            if (ptr == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            :(int idx, string f) = self.__get_placeholder(fmt, next_idx, ptr);
            next_idx = ptr + 1;
            output_array[out_idx++] = self.__get_string_value(args, cache, idx, f);
        }
        return join("", output_array);
    }

    /* Internal Helper Methods
    */

    // Get index/format information from a {...} sequence
    function __get_placeholder(string fmt, int start_idx, int end_idx)
    {
        string f = substr(fmt, start_idx, end_idx - start_idx);
        var p = split(":", f);
        int num_p = elements(p);
        if (p == 0 || p > 2)
            Rosella.Error.invalid(__FUNCTION__, "Invalid format '%s'", f);
        if (p == 1)
            return int(p[0]), "s";
        if (p == 2)
            return int(p[0]), string(p[1]);
    }

    // Get the (possibly cached) string value for the given argument with the
    // given format
    function __get_string_value(var args, var cache, int idx, string fmt)
    {
        string key = string(idx) + ":" + fmt;
        if (exists cache[key])
            return cache[key];
        var obj = args[idx];
        var converter = self.__get_converter(obj);
        string value = converter.convert(obj, fmt);
        cache[key] = value;
        return value;
    }

    // Get a suitable converter for the given object
    // Check from most specific first to least specific last
    function __get_converter(var obj)
    {
        var obj_c = typeof(obj);
        var converter = null;
        if (exists self.type_converters[obj_c])
            return self.type_converters[obj_c];
        for (string role in self.role_converters) {
            if (does(obj, role))
                return self.role_converters[role];
        }
        return self.default_converter;
    }
}
