/* String Formatting Engine
    This class implements a variant of .NET-style String.Format formatting. It
    is like an alternate syntax/semantic for sprintf.
*/
class Rosella.String.FormatPrinter : Rosella.StringFormatter
{
    var default_formatter;

    // Constructor
    function FormatPrinter()
    {
        self.default_formatter = new Rosella.StringFormatter();
    }

    // Add a converter for a specific type
    function add_type_formatter(var type, var converter)
    {
        Rosella.StringFormatter.register_type_formatter(type, converter);
    }

    // Add a converter for a role
    function add_role_formatter(string role, var converter)
    {
        Rosella.StringFormatter.register_role_formatter(role, converter);
    }

    // Format the string using an array of arguments
    function format(var args, string fmt)
    {
        int cclass_numeric = Rosella.String.get_character_class("numeric");
        string raw_args[] = [];
        var cache = Rosella.get_string_string_hash();

        string output_array[] = [];
        int fmt_len = length(fmt);
        int out_idx = 0;
        int cur_idx = 0;
        int next_idx = 0;

        while(cur_idx < fmt_len) {
            cur_idx = next_idx;
            next_idx = index_of_substr_from(fmt, "{", cur_idx);
            if (cur_idx != next_idx)
                output_array[out_idx++] = substr(fmt, cur_idx, next_idx - cur_idx);

            if (next_idx == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            // Else, we've found a format sequence.
            next_idx++;
            if (fmt[next_idx] == "{") {
                output_array[out_idx++] = "{";
                next_idx++;
                continue;
            }
            int ptr = index_of_substr_from(fmt, "}", next_idx);
            if (ptr == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            :(int idx, string f) = self.__get_placeholder(fmt, next_idx, ptr);
            next_idx = ptr + 1;
            output_array[out_idx++] = self.__get_string_value(args, cache, idx, f);
        }
        return join("", output_array);
    }

    // Format the string, using a single argument object.
    function format_obj(var obj, string fmt)
    {
        int cclass_numeric = Rosella.String.get_character_class("numeric");
        string raw_args[] = [];
        var cache = Rosella.get_string_string_hash();

        string output_array[] = [];
        int fmt_len = length(fmt);
        int out_idx = 0;
        int cur_idx = 0;
        int next_idx = 0;

        while(cur_idx < fmt_len) {
            cur_idx = next_idx;
            next_idx = index_of_substr_from(fmt, "{", cur_idx);
            if (cur_idx != next_idx)
                output_array[out_idx++] = substr(fmt, cur_idx, next_idx - cur_idx);

            if (next_idx == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            // Else, we've found a format sequence.
            next_idx++;
            if (fmt[next_idx] == "{") {
                output_array[out_idx++] = "{";
                continue;
            }
            int ptr = index_of_substr_from(fmt, "}", next_idx);
            if (ptr == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            :(string attr_name, string f) = self.__get_obj_attribute(fmt, next_idx, ptr);
            next_idx = ptr + 1;
            output_array[out_idx++] = self.__get_obj_value(obj, cache, attr_name, f);
        }
        return join("", output_array);
    }

    /* Internal Helper Methods
    */

    // Get the attribute/format from a {...} sequence
    function __get_obj_attribute(string fmt, int start_idx, int end_idx)
    {
        string f = substr(fmt, start_idx, end_idx - start_idx);
        var p = split(":", f);
        int num_p = elements(p);

        if (p == 1)
            return string(p[0]), "s";
        if (p == 2)
            return string(p[0]), string(p[1]);
        Rosella.Error.error("Invalid format '%s'", f);
    }

    // Get index/format information from a {...} sequence
    function __get_placeholder(string fmt, int start_idx, int end_idx)
    {
        string f = substr(fmt, start_idx, end_idx - start_idx);
        var p = split(":", f);
        int num_p = elements(p);
        if (p == 0 || p > 2)
            Rosella.Error.error("Invalid format '%s'", f);
        if (p == 1)
            return int(p[0]), "s";
        if (p == 2)
            return int(p[0]), string(p[1]);
    }

    // Get a formatted value for format_obj. Take an attr/method name and a
    // format string to pass to the default converter of that type
    function __get_obj_value(var obj, var cache, string attr, string fmt)
    {
        // If we have it in cache, return it
        string key = attr + ":" + fmt;
        if (exists cache[key])
            return cache[key];

        // Get the value from the object. It may be a hash value, an attribute
        // or a 0-arity method call return value
        var value = null;
        int has_value = false;
        if (does(obj, "hash")) {
            if (exists obj[attr]) {
                value = obj[attr];
                has_value = true;
            }
        } else {
            try {
                value = obj.*attr;
                has_value = true;
            } catch(e){ }
            if (has_value == false) {
                try {
                    value = obj.*attr();
                    has_value = true;
                } catch(e) { }
            }
        }
        if (!has_value) {
            cache[key] = "";
            return "";
        }
        string formatted = self.__convert_format(value, fmt);
        return formatted;
    }

    // Get the (possibly cached) string value for the given argument with the
    // given format
    function __get_string_value(var args, var cache, int idx, string fmt)
    {
        string key = string(idx) + ":" + fmt;
        if (exists cache[key])
            return cache[key];
        var obj = args[idx];
        string value = self.__convert_format(obj, fmt);
        cache[key] = value;
        return value;
    }

    // Format an object according to the given format. Search for the formatter
    // associated with that object type, or use the default formatter if none
    // are available
    function __convert_format(var obj, string fmt)
    {
        var converter = Rosella.StringFormatter.get_formatter(obj);
        if (converter == null)
            converter = self.default_formatter;
        return converter.format(obj, fmt);
    }
}
