/* String Formatting Engine
    This class implements a variant of .NET-style String.Format formatting. It
    is like an alternate syntax/semantic for sprintf.
*/
class Rosella.String.FormatPrinter : Rosella.StringFormatter
{
    var default_formatter;

    // Constructor
    function FormatPrinter()
    {
        self.default_formatter = new Rosella.StringFormatter();
    }

    // Add a converter for a specific type
    function add_type_formatter(var type, var converter)
    {
        Rosella.StringFormatter.register_type_formatter(type, converter);
    }

    // Add a converter for a role
    function add_role_formatter(string role, var converter)
    {
        Rosella.StringFormatter.register_role_formatter(role, converter);
    }

    // Format the string using an array of arguments
    function format(var args, string fmt)
    {
        int cclass_numeric = Rosella.String.get_character_class("numeric");
        string raw_args[] = [];
        var cache = Rosella.get_string_string_hash();

        string output_array[] = [];
        int fmt_len = length(fmt);
        int out_idx = 0;
        int cur_idx = 0;
        int next_idx = 0;

        while(cur_idx < fmt_len) {
            cur_idx = next_idx;
            next_idx = index_of_substr_from(fmt, "{", cur_idx);
            if (cur_idx != next_idx)
                output_array[out_idx++] = substr(fmt, cur_idx, next_idx - cur_idx);

            if (next_idx == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            // Else, we've found a format sequence.
            next_idx++;
            if (fmt[next_idx] == "{") {
                output_array[out_idx++] = "{";
                continue;
            }
            int ptr = index_of_substr_from(fmt, "}", next_idx);
            if (ptr == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            :(int idx, string f) = self.__get_placeholder(fmt, next_idx, ptr);
            next_idx = ptr + 1;
            output_array[out_idx++] = self.__get_string_value(args, cache, idx, f);
        }
        return join("", output_array);
    }

    // Format the string, using a single argument object.
    function format_obj(var obj, string fmt)
    {
        int cclass_numeric = Rosella.String.get_character_class("numeric");
        string raw_args[] = [];
        var cache = Rosella.get_string_string_hash();

        string output_array[] = [];
        int fmt_len = length(fmt);
        int out_idx = 0;
        int cur_idx = 0;
        int next_idx = 0;

        while(cur_idx < fmt_len) {
            cur_idx = next_idx;
            next_idx = index_of_substr_from(fmt, "{", cur_idx);
            if (cur_idx != next_idx)
                output_array[out_idx++] = substr(fmt, cur_idx, next_idx - cur_idx);

            if (next_idx == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            // Else, we've found a format sequence.
            next_idx++;
            if (fmt[next_idx] == "{") {
                output_array[out_idx++] = "{";
                continue;
            }
            int ptr = index_of_substr_from(fmt, "}", next_idx);
            if (ptr == -1) {
                output_array[out_idx++] = substr(fmt, cur_idx);
                break;
            }
            string attr_name = self.__get_obj_attribute(fmt, next_idx, ptr);
            next_idx = ptr + 1;
            output_array[out_idx++] = self.__get_obj_value(obj, cache, attr_name);
        }
        return join("", output_array);
    }

    /* Internal Helper Methods
    */

    function __get_obj_attribute(string fmt, int start_idx, int end_idx)
    {
        return substr(fmt, start_idx, end_idx - start_idx);
    }

    // Get index/format information from a {...} sequence
    function __get_placeholder(string fmt, int start_idx, int end_idx)
    {
        string f = substr(fmt, start_idx, end_idx - start_idx);
        var p = split(":", f);
        int num_p = elements(p);
        if (p == 0 || p > 2)
            Rosella.Error.error("Invalid format '%s'", f);
        if (p == 1)
            return int(p[0]), "s";
        if (p == 2)
            return int(p[0]), string(p[1]);
    }

    function __get_obj_value(var obj, var cache, string attr)
    {
        if (exists cache[attr])
            return cache[attr];
        string value = null;
        try {
            value = obj.*attr;
        } catch(e){ }
        if (value == null) {
            try {
                value = obj.*attr();
            } catch(e) { }
        }
        if (value == null)
            value = "";
        cache[attr] = value;
        return value;
    }

    // Get the (possibly cached) string value for the given argument with the
    // given format
    function __get_string_value(var args, var cache, int idx, string fmt)
    {
        string key = string(idx) + ":" + fmt;
        if (exists cache[key])
            return cache[key];
        var obj = args[idx];
        var converter = Rosella.StringFormatter.get_formatter(obj);
        if (converter == null)
            converter = self.default_formatter;
        string value = converter.format(obj, fmt);
        cache[key] = value;
        return value;
    }
}
