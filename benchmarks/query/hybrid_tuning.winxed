$loadlib "math_ops";
const int N = 100000;
const int SORT_TRANSITION = 6;

function sorted_N_list(var d)
{
    for (int i = N - 1; i >= 0; i--)
        d[i] = i;
}

function reverse_sorted_N_list(var d)
{
    for (int i = N - 1; i >= 0; i--)
        d[i] = N - i;
}

function random_N_list(var d)
{
    for (int i = N - 1; i >= 0; i--) {
        int x;
        ${ rand x };
        d[i] = x;
    }
}

function display_result(string name, var result)
{
    say(name);
    print("\t");
    say(result);
}

function compare(var a, var b)
{
    if (a > b) return 1;
    if (a == b) return 0;
    return -1;
}

function qsort_with_insertion(var d, int s, int n, var cmp)
{
    int last = n-1;
    while (last > s) {
        if ((last - s) < SORT_TRANSITION) {
            insertion_sort(d, s, n, cmp);
            return;
        }
        int pivot = s + int((n - s) / 2);
        int store = s;
        var tmp;

        var piv = d[pivot];
        d[pivot] = d[last];
        d[last] = piv;

        for(int ix = s; ix < last; ix++) {
            if (cmp(d[ix], piv) < 0) {
                tmp = d[store];
                d[store] = d[ix];
                d[ix] = tmp;
                store++;
            }
        }

        tmp = d[last];
        d[last] = d[store];
        d[store] = tmp;
        pivot = store;
        qsort_with_insertion(d, s, pivot, cmp);
        s = pivot + 1;
    }
}

function insertion_sort(var d, int s, int n, var cmp)
{
    for (int x = s + 1; x < n; x++)
    {
        var val = d[x];
        int j = x - 1;
        while (j >= 0 && cmp(val, d[j]) < 0)
        {
            d[j + 1] = d[j];
            j--;
        }
        d[j + 1] = val;
    }
}

function qi_with_inline(var d, int s, int n, var cmp)
{
    int last = n-1;
    while (last > s) {
        if ((last - s) < SORT_TRANSITION) {
            for (int x = s + 1; x < n; x++)
            {
                var val = d[x];
                int j = x - 1;
                while (j >= 0 && cmp(val, d[j]) < 0)
                {
                    d[j + 1] = d[j];
                    j--;
                }
                d[j + 1] = val;
            }
            return;
        }
        int pivot = s + int((n - s) / 2);
        int store = s;
        var tmp;

        var piv = d[pivot];
        d[pivot] = d[last];
        d[last] = piv;

        for(int ix = s; ix < last; ix++) {
            if (cmp(d[ix], piv) < 0) {
                tmp = d[store];
                d[store] = d[ix];
                d[ix] = tmp;
                store++;
            }
        }

        tmp = d[last];
        d[last] = d[store];
        d[store] = tmp;
        pivot = store;
        qi_with_inline(d, s, pivot, cmp);
        s = pivot + 1;
    }
}


function main[main]()
{
    var rosella = load_packfile("rosella/core.pbc");
    var(Rosella.initialize_rosella)("benchmark", "query");
    say(sprintf("N = %d", [N]));
    var d = [];
    var result;
    var base;

    say("\nFORWARD-SORTED (PRESORTED) BENCHMARKS\n");

    sorted_N_list(d);
    base = Rosella.Benchmark.benchmark(function() { using static compare; Rosella.Query.as_queryable(d).sort_self(compare); });
    display_result("sort with Rosella Query", base);

    sorted_N_list(d);
    result = Rosella.Benchmark.benchmark(function() { using static compare; qsort_with_insertion(d, 0, elements(d), compare); });
    result.set_base_time(base.time());
    display_result("qsort+insertion sort", result);

    sorted_N_list(d);
    result = Rosella.Benchmark.benchmark(function() { using static compare; qi_with_inline(d, 0, elements(d), compare); });
    result.set_base_time(base.time());
    display_result("inlined hybrid sort", result);

    say("\nREVERSE-SORTED BENCHMARKS\n");

    reverse_sorted_N_list(d);
    base = Rosella.Benchmark.benchmark(function() { using static compare; Rosella.Query.as_queryable(d).sort_self(compare); });
    display_result("sort with Rosella Query", base);

    reverse_sorted_N_list(d);
    result = Rosella.Benchmark.benchmark(function() { using static compare; qsort_with_insertion(d, 0, elements(d), compare); });
    result.set_base_time(base.time());
    display_result("qsort+insertion sort", result);

    reverse_sorted_N_list(d);
    result = Rosella.Benchmark.benchmark(function() { using static compare; qi_with_inline(d, 0, elements(d), compare); });
    result.set_base_time(base.time());
    display_result("inlined hybrid sort", result);

    say("\nRANDOM DATA BENCHMARKS\n");

    random_N_list(d);
    base = Rosella.Benchmark.benchmark(function() { using static compare; Rosella.Query.as_queryable(d).sort_self(compare); });
    display_result("sort with Rosella Query", base);

    random_N_list(d);
    result = Rosella.Benchmark.benchmark(function() { using static compare; qsort_with_insertion(d, 0, elements(d), compare); });
    result.set_base_time(base.time());
    display_result("qsort+insertion sort", result);

    random_N_list(d);
    result = Rosella.Benchmark.benchmark(function() { using static compare; qi_with_inline(d, 0, elements(d), compare); });
    result.set_base_time(base.time());
    display_result("inlined hybrid sort", result);
}
