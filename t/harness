INIT {
    my $core := pir::load_bytecode__PS("rosella/core.pbc");
    Rosella::initialize_rosella("harness");
}

my @argv := pir::getinterp__P()[2];
my $dummy := pir::shift__PP(@argv);

my $factory := Rosella::construct(Rosella::Harness::TestRun::Factory);
my $harness := Rosella::construct(Rosella::Harness);
my $testview := $harness.default_view();

my @test_runs := [];

if (pir::elements(@argv)) {
    for @argv -> $dir {
        my $type := "NQP";
        if ($dir eq "template") {
            $type := "Winxed";
        }
        $factory.add_test_dirs($type, "t/$dir", :recurse(1));
        my $run := $factory.create();
        pir::push(@test_runs, $run);
        $testview.add_run($run, 0);
    }
} else {
    # Core tests (If these fail, we don't attempt anything else)
    $factory.add_test_dirs("NQP",
        "t/core"
    );
    my $run := $factory.create();
    pir::push(@test_runs, $run);
    $testview.add_run($run, 0);

    # Winxed tests
    # These are some basic sanity tests to prove we can also write tests in
    # winxed.
    $factory.add_test_dirs("Winxed",
        "t/winxed_test"
    );
    $run := $factory.create();
    pir::push(@test_runs, $run);
    $testview.add_run($run, 0);

    # Stable library tests
    $factory.add_test_dirs("NQP",
        "t/tap_harness",    # "harness", can't shadow t/harness program
        "t/test",
        "t/action",
        "t/container",
        "t/event",
        "t/query",
        "t/proxy",
        "t/mockobject",
        "t/memoize",
        "t/filesystem",
        "t/path",
        "t/string",
        :recurse(1)
    );
    $run := $factory.create();
    pir::push(@test_runs, $run);
    $testview.add_run($run, 0);

    # Unstable library tests
    $factory.add_test_dirs("NQP",
        "t/prototype",
        :recurse(1)
    );
    $factory.add_test_dirs("Winxed",
        "t/template",
        :recurse(1)
    );
    $run := $factory.create();
    pir::push(@test_runs, $run);
    $testview.add_run($run, 0);
}

for @test_runs -> $run {
    my $result := $harness.run($run, $testview);
    if (!$result) {
        $testview.show_results();
        pir::exit(1);
    }
}
$testview.show_results();
#pir::exit(0);
