class Rosella.FileSystem.Directory;
namespace Rosella.Query { extern function as_stream; }
namespace Rosella.String {extern function pad_end; }

function show_help(var args)
{
    Rosella.IO.sayf("Usage: %s [directory]", args[0]);
    exit(0);
}

function ls_main(var args)
{
    var dirname = ".";
    for(var i = 1; i < args; i++) {
        var arg = args[i];
        if(arg == "-h" || arg == "--help")
            show_help(args);
        else
            dirname = arg;
    }
    var dir = new Rosella.FileSystem.Directory(dirname);
    if(!dir.exists()) {
        Rosella.IO.sayf("ls: cannot access %s: No such file or directory", dirname);
        exit(-1);
    }

    Rosella.Query.as_stream(dir)
        .map(function(e) { return e.short_name(); })
        .sort()
        .map(function(e) { return Rosella.String.pad_end(e, 20); })
        .foreach(function(string e) { Rosella.IO.printf("%s\t", e); })
        .tap(3, function() { say(""); })
        .execute();

    say("");
}

function main[main](var args)
{
    var rosella = load_packfile("rosella/core.pbc");
    var(Rosella.initialize_rosella)("filesystem", "query", "string");
    ls_main(args);
}
